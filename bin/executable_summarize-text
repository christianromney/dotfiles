#!/usr/bin/env fish
argparse 'm/model=' 'o/output=' -- $argv
or return

# Default to gemma3 if no model specified
set -l model gemma3
if set -q _flag_model
  set model $_flag_model
end

# Read input: from file or stdin
if test (count $argv) -eq 0; or test "$argv[1]" = "-"
  # Read from stdin
  set text (cat | uniq)
else
  # Read from file
  set text (cat $argv[1] | uniq)
end

# Build prompt
set -l prompt "Prepare an executive summary of the most important points in the following text"
if set -q argv[2]
  set prompt $argv[2] $prompt
end

# Generate summary and output based on flag
if set -q _flag_output
  if test "$_flag_output" = "-"
    # Explicit stdout
    ollama run $model "$prompt: $text"
  else
    # Write to file
    ollama run $model "$prompt: $text" > $_flag_output
  end
else
  # Default: stdout
  ollama run $model "$prompt: $text"
end
